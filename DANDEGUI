(DEFINE-FILE-INFO PACKAGE (DEFPACKAGE "DANDEGUI" (USE "LISP" "XCL") (NICKNAMES "GUI")) READTABLE 
"XCL" BASE 10)

(IL:FILECREATED "26-Apr-2025 16:47:54" IL:|{DSK}<home>paolo>il>dandegui>DANDEGUI.;7| 6003   

      :EDIT-BY "PA"

      :CHANGES-TO (IL:FUNCTIONS WITH-WRITE-ENABLED)

      :PREVIOUS-DATE "26-Apr-2025 13:45:21" IL:|{DSK}<home>paolo>il>dandegui>DANDEGUI.;6|)


(IL:PRETTYCOMPRINT IL:DANDEGUICOMS)

(IL:RPAQQ IL:DANDEGUICOMS
          ((IL:* IL:A IL:GUI IL:|library| IL:|for| IL:|simple| IL:|text| IL:|and| IL:|graphics| 
                 IL:|output.|)
           (FILE-ENVIRONMENTS IL:DANDEGUI)
           (IL:P (DEFPACKAGE "DANDEGUI" (:USE "LISP" "XCL")
                        (:NICKNAMES "GUI")
                        (:EXPORT OPEN-WINDOW-STREAM CLEAR-WINDOW WINDOW-TITLE WITH-WINDOW-STREAM 
                               WITH-OUTPUT-TO-WINDOW)))
           (IL:FUNCTIONS CLEAR-WINDOW OPEN-WINDOW-STREAM WINDOW-TITLE WITH-OUTPUT-TO-WINDOW 
                  WITH-WINDOW-STREAM WITH-WRITE-ENABLED)
           (IL:SEDIT-FORMATS WITH-OUTPUT-TO-WINDOW WITH-WINDOW-STREAM)
           (IL:SETFS WINDOW-TITLE)))



(IL:* IL:A IL:GUI IL:|library| IL:|for| IL:|simple| IL:|text| IL:|and| IL:|graphics| IL:|output.|)


(DEFINE-FILE-ENVIRONMENT IL:DANDEGUI :PACKAGE (DEFPACKAGE "DANDEGUI" (:USE "LISP" "XCL")
                                                     (:NICKNAMES "GUI"))
   :READTABLE "XCL"
   :COMPILER :COMPILE-FILE)

(DEFPACKAGE "DANDEGUI" (:USE "LISP" "XCL")
       (:NICKNAMES "GUI")
       (:EXPORT OPEN-WINDOW-STREAM CLEAR-WINDOW WINDOW-TITLE WITH-WINDOW-STREAM WITH-OUTPUT-TO-WINDOW
              ))

(DEFUN CLEAR-WINDOW (STREAM)                           (IL:* IL:\; "Edited 26-Apr-2025 13:33 by PA")
   "Delete all the text of the window associated with STREAM."
   (WITH-WRITE-ENABLED (STR STREAM)
          (IL:TEDIT.DELETE STR 1 (IL:TEDIT.NCHARS STR))))

(DEFUN OPEN-WINDOW-STREAM (&KEY (TITLE "Untitled"))    (IL:* IL:\; "Edited 13-Apr-2025 17:38 by PA")
   "Open a new window and return a text stream to send output to."
                                                       (IL:* IL:\; "Edited 12-Apr-2025 18:28 by PA")
   (LET ((STREAM (IL:TEXTSTREAM (IL:TEDIT
                                 NIL NIL NIL
                                 `(IL:FONT (IL:TERMINAL 10)
                                         IL:HISTORY IL:OFF IL:READONLY IL:QUIET 

                                         (IL:* IL:|;;| 
                                "A dummy function to suppress TEdit's  left/middle button title menu")

                                         IL:TITLEMENUFN
                                         ,#'(LAMBDA (WINDOW)
                                                   (DECLARE (IGNORE WINDOW))
                                                   T))))))
        (IL:WINDOWPROP (IL:WFROMDS STREAM)
               'IL:TITLE TITLE)
        STREAM))

(DEFUN WINDOW-TITLE (STREAM)                           (IL:* IL:\; "Edited 20-Apr-2025 13:02 by PA")
   "Return the title of the window associated with STREAM."
   (IL:WINDOWPROP (IL:WFROMDS STREAM)
          'IL:TITLE))

(DEFMACRO WITH-OUTPUT-TO-WINDOW ((VAR &KEY TITLE)
                                 &BODY BODY)           (IL:* IL:\; "Edited 16-Apr-2025 14:17 by PA")
   "WITH-OUTPUT-TO-WINDOW (VAR &KEY TITLE) BODY
Perform the operations in BODY with VAR bound to a new window stream.
Opens a new window with title TITLE, binds VAR to the TEXTSTREAM associated with the window, and executes BODY in this context. Returns the value of the last form of BODY."
   `(WITH-WINDOW-STREAM 

           (IL:* IL:|;;| 
  "If :TITLE is NIL the title bar disappear, so we must pass something non NIL to OPEN-WINDOW-STREAM")

           (,VAR (OPEN-WINDOW-STREAM :TITLE (OR ,TITLE "Untitled")))
           ,@BODY))

(DEFMACRO WITH-WINDOW-STREAM ((VAR STREAM)
                              &BODY BODY)              (IL:* IL:\; "Edited 16-Apr-2025 13:25 by PA")
   "Perform the operations in BODY with VAR bound to a window STREAM.
Evaluates the forms in BODY in a context in which VAR is bound to STREAM which must already exist, then returns the value of the last form of BODY."
   `(WITH-WRITE-ENABLED (,VAR ,STREAM)
           ,@BODY))

(DEFMACRO WITH-WRITE-ENABLED ((VAR STREAM)
                              &BODY BODY)              (IL:* IL:\; "Edited 26-Apr-2025 16:44 by PA")
                                                       (IL:* IL:\; "Edited 15-Apr-2025 19:34 by PA")
   "Perform the operations in BODY on STREAM after enabling write access, then set it to readonly.
Evaluates the forms in BODY in a context in which STREAM is bound to VAR and write access is enabled for STREAM, then sets STREAM back to readonly and returns the value of the last form of BODY."
   (ONCE-ONLY (STREAM)
          `(UNWIND-PROTECT
               (LET ((,VAR ,STREAM))
                    (IL:TEXTPROP ,STREAM 'IL:READONLY NIL)

                    (IL:* IL:|;;| 
                    "Ensure new text is appended to the end by setting the stream pointer to EOF=-1")

                    (IL:SETFILEPTR ,STREAM -1)
                    ,@BODY)

               (IL:* IL:|;;| 
           "Mark buffer  as not changed so the user is not prompted when to save when quitting TEdit")

               (IL:TEDIT.STREAMCHANGEDP ,STREAM T)
               (IL:TEXTPROP ,STREAM 'IL:READONLY 'IL:QUIET))))

(SEDIT:DEF-LIST-FORMAT WITH-OUTPUT-TO-WINDOW WITH-OPEN-STREAM)

(SEDIT:DEF-LIST-FORMAT WITH-WINDOW-STREAM WITH-OPEN-STREAM)

(DEFSETF WINDOW-TITLE (STREAM) (TITLE)
   "Set to TITLE the title of the window associated with STREAM."

   (IL:* IL:|;;| "TITLE must  not be NIL otherwise the title bar of the window gets hidden")

   `(IL:WINDOWPROP (IL:WFROMDS ,STREAM)
           'IL:TITLE
           (OR ,TITLE "Untitled")))
(IL:DECLARE\: IL:DONTCOPY
  (IL:FILEMAP (NIL (1651 1924 (CLEAR-WINDOW 1651 . 1924)) (1926 2960 (OPEN-WINDOW-STREAM 1926 . 2960)) (
2962 3194 (WINDOW-TITLE 2962 . 3194)) (3196 3910 (WITH-OUTPUT-TO-WINDOW 3196 . 3910)) (3912 4349 (
WITH-WINDOW-STREAM 3912 . 4349)) (4351 5535 (WITH-WRITE-ENABLED 4351 . 5535)))))
IL:STOP
